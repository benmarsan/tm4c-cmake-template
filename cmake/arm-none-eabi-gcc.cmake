set(CMAKE_SYSTEM_NAME               Generic)
set(CMAKE_SYSTEM_PROCESSOR          arm)
set(CMAKE_CROSSCOMPILING TRUE)

set(TOOLCHAIN_PREFIX arm-none-eabi-)

# On Windows, executables end in .exe (ex: arm-none-eabi-gcc.exe)
if(MINGW OR CYGWIN OR WIN32)
    set(HOST_EXECUTABLE_SUFFIX ".exe")
elseif(UNIX OR APPLE)
    set(HOST_EXECUTABLE_SUFFIX "")
endif()

function(find_armgcc)
    if(MINGW OR CYGWIN OR WIN32)
        set(UTIL_SEARCH_CMD where.exe)
    elseif(UNIX OR APPLE)
        set(UTIL_SEARCH_CMD which)
    endif()

    if(EXISTS ${CMAKE_C_COMPILER})
        set(BINUTILS_PATH ${CMAKE_C_COMPILER})
    elseif(EXISTS ${CMAKE_ASM_COMPILER})
        set(BINUTILS_PATH ${CMAKE_ASM_COMPILER})
    elseif(EXISTS ${CMAKE_CXX_COMPILER})
        set(BINUTILS_PATH ${CMAKE_CXX_COMPILER})
    else()
        execute_process(
            COMMAND ${UTIL_SEARCH_CMD} ${TOOLCHAIN_PREFIX}gcc${HOST_EXECUTABLE_SUFFIX}
            OUTPUT_VARIABLE BINUTILS_PATH
            OUTPUT_STRIP_TRAILING_WHITESPACE
        )
    endif()

    # Use only the topmost path, if more than one is found
    string(REGEX REPLACE "\n.+" "" BINUTILS_PATH "${BINUTILS_PATH}")

    set(BINUTILS_PATH ${BINUTILS_PATH} PARENT_SCOPE)
endfunction()

if(NOT ARMGCC_TOOLCHAIN_PATH)
    find_armgcc()
elseif(EXISTS "${ARMGCC_TOOLCHAIN_PATH}/${TOOLCHAIN_PREFIX}gcc${HOST_EXECUTABLE_SUFFIX}")
    set(BINUTILS_PATH "${ARMGCC_TOOLCHAIN_PATH}/${TOOLCHAIN_PREFIX}gcc${HOST_EXECUTABLE_SUFFIX}")
elseif(EXISTS "${ARMGCC_TOOLCHAIN_PATH}/bin/${TOOLCHAIN_PREFIX}gcc${HOST_EXECUTABLE_SUFFIX}")
    set(BINUTILS_PATH "${ARMGCC_TOOLCHAIN_PATH}/bin/${TOOLCHAIN_PREFIX}gcc${HOST_EXECUTABLE_SUFFIX}")
else()
    message(FATAL_ERROR "No ${TOOLCHAIN_PREFIX}gcc${HOST_EXECUTABLE_SUFFIX} found at ${ARMGCC_TOOLCHAIN_PATH}")
endif()

get_filename_component(ARMGCC_TOOLCHAIN_PATH "${BINUTILS_PATH}" DIRECTORY)

# Without that flag CMake is not able to pass test compilation check
set(CMAKE_TRY_COMPILE_TARGET_TYPE   STATIC_LIBRARY)

# Workaround for VSCode CMake Tools that needs the compiler paths in the CMake cache
set(CMAKE_C_COMPILER "${ARMGCC_TOOLCHAIN_PATH}/${TOOLCHAIN_PREFIX}gcc${HOST_EXECUTABLE_SUFFIX}" CACHE FILEPATH "C Compiler path")
set(CMAKE_ASM_COMPILER "${ARMGCC_TOOLCHAIN_PATH}/${TOOLCHAIN_PREFIX}gcc${HOST_EXECUTABLE_SUFFIX}" CACHE FILEPATH "ASM Compiler path")
set(CMAKE_CXX_COMPILER "${ARMGCC_TOOLCHAIN_PATH}/${TOOLCHAIN_PREFIX}g++${HOST_EXECUTABLE_SUFFIX}" CACHE FILEPATH "C++ Compiler path")

set(CMAKE_AR "${ARMGCC_TOOLCHAIN_PATH}/${TOOLCHAIN_PREFIX}ar${HOST_EXECUTABLE_SUFFIX}" CACHE INTERNAL "ar tool")
set(CMAKE_GCOV "${ARMGCC_TOOLCHAIN_PATH}/${TOOLCHAIN_PREFIX}gcov${HOST_EXECUTABLE_SUFFIX}" CACHE INTERNAL "size tool")
set(CMAKE_LINKER "${ARMGCC_TOOLCHAIN_PATH}/${TOOLCHAIN_PREFIX}ld${HOST_EXECUTABLE_SUFFIX}" CACHE INTERNAL "ld tool")
set(CMAKE_OBJCOPY "${ARMGCC_TOOLCHAIN_PATH}/${TOOLCHAIN_PREFIX}objcopy${HOST_EXECUTABLE_SUFFIX}" CACHE INTERNAL "objcopy tool")
set(CMAKE_OBJDUMP "${ARMGCC_TOOLCHAIN_PATH}/${TOOLCHAIN_PREFIX}objdump${HOST_EXECUTABLE_SUFFIX}" CACHE INTERNAL "objdump tool")
set(CMAKE_RANLIB "${ARMGCC_TOOLCHAIN_PATH}/${TOOLCHAIN_PREFIX}ranlib${HOST_EXECUTABLE_SUFFIX}" CACHE INTERNAL "size tool")
set(CMAKE_SIZE_UTIL "${ARMGCC_TOOLCHAIN_PATH}/${TOOLCHAIN_PREFIX}size${HOST_EXECUTABLE_SUFFIX}" CACHE INTERNAL "size tool")
set(CMAKE_STRIP "${ARMGCC_TOOLCHAIN_PATH}/${TOOLCHAIN_PREFIX}strip${HOST_EXECUTABLE_SUFFIX}" CACHE INTERNAL "size tool")

set(CMAKE_SYSROOT "${ARMGCC_TOOLCHAIN_PATH}/../arm-none-eabi")
set(CMAKE_FIND_ROOT_PATH "${BINUTILS_PATH}")
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
